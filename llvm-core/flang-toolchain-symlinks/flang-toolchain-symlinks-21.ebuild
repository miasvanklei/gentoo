# Copyright 1999-2025 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

inherit multilib

DESCRIPTION="Symlinks to use Flang on gfortran-free system"
HOMEPAGE="https://wiki.gentoo.org/wiki/Project:LLVM"
S="${WORKDIR}"

LICENSE="public-domain"
SLOT="${PV}"
KEYWORDS=""
IUSE="multilib-symlinks +native-symlinks"

RDEPEND="
	llvm-core/flang:${SLOT}
"

src_install() {
	use native-symlinks || return

	local chosts=( "${CHOST}" )
	if use multilib-symlinks; then
		local abi
		for abi in $(get_all_abis); do
			chosts+=( "$(get_abi_CHOST "${abi}")" )
		done
	fi

	local dest=/usr/lib/llvm/${SLOT}/bin
	dodir "${dest}"
	dosym flang "${dest}/gfortran"
	for chost in "${chosts[@]}"; do
		dosym flang "${dest}/${chost}-flang"
		dosym flang "${dest}/${chost}-gfortran"
	done
}
