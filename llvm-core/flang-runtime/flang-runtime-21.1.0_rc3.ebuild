# Copyright 1999-2025 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

inherit multilib-build toolchain-funcs

DESCRIPTION="Meta-ebuild for flang runtime libraries"
HOMEPAGE="https://llvm.org/"

S="${WORKDIR}"

LICENSE="metapackage"
SLOT="${PV%%.*}"
KEYWORDS=""
IUSE="default-compiler-rt default-lld llvm-libunwind"

RDEPEND="
	default-compiler-rt? (
		~llvm-runtimes/compiler-rt-${PV}:${SLOT}[abi_x86_32(+)?,abi_x86_64(+)?]
		llvm-libunwind? ( llvm-runtimes/libunwind[static-libs] )
		!llvm-libunwind? ( sys-libs/libunwind[static-libs] )
	)
	!default-compiler-rt? ( sys-devel/gcc )
	default-lld? ( ~llvm-core/lld-${PV} )
	!default-lld? ( sys-devel/binutils )
"

_doflang_cfg() {
	local triple="${1}"

	newins - flang.cfg <<-EOF
		# This file is initially generated by llvm-core/flang-runtime.
		# It is used to control the default runtimes using by flang.

		--rtlib=$(usex default-compiler-rt compiler-rt libgcc)
		--unwindlib=$(usex default-compiler-rt libunwind libgcc)
		-fuse-ld=$(usex default-lld lld bfd)
	EOF

	local tool
	for tool in "${triple}"-flang "${triple}"-gfortran gfortran; do
		dosym flang.cfg "/etc/clang/${SLOT}/${tool}.cfg"
	done

	# Install symlinks for triples with other vendor strings since some
	# programs insist on mangling the triple.
	local vendor
	for vendor in gentoo pc unknown; do
		local vendor_triple="${triple%%-*}-${vendor}-${triple#*-*-}"
		for tool in flang gfortran; do
			if [[ ! -f "${ED}/etc/clang/${SLOT}/${vendor_triple}-${tool}.cfg" ]]; then
				dosym "${triple}-${tool}.cfg" "/etc/clang/${SLOT}/${vendor_triple}-${tool}.cfg"
			fi
		done
	done
}

doflang_cfg() {
	local triple=$(get_abi_CHOST "${abi}")

	_doflang_cfg ${triple}

	# LLVM may have different arch names in some cases. For example in x86
	# profiles the triple uses i686, but llvm will prefer i386 if invoked
	# with "clang" on x86 or "clang -m32" on x86_64. The gentoo triple will
	# be used if invoked through ${CHOST}-clang{,++,-cpp} though.
	#
	# To make sure the correct triples are installed,
	# see Triple::getArchTypeName() in llvm/lib/TargetParser/Triple.cpp
	# and compare with CHOST values in profiles.

	local abi=${triple%%-*}
	case ${abi} in
		armv4l|armv4t|armv5tel|armv6j|armv7a)
			_doflang_cfg ${triple/${abi}/arm}
			;;
		i686)
			_doflang_cfg ${triple/${abi}/i386}
			;;
		sparc)
			_doflang_cfg ${triple/${abi}/sparcel}
			;;
		sparc64)
			_doflang_cfg ${triple/${abi}/sparcv9}
			;;
	esac
}

src_install() {
	insinto "/etc/clang/${SLOT}"

	multilib_foreach_abi doflang_cfg
}
